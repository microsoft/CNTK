load=ndlMacros
run=DNN

ndlMacros = [
    ImageW = 224
    ImageH = 224
    ImageC = 3
    LabelDim = 1000

    features = ImageInput(ImageW, ImageH, ImageC, tag = feature, imageLayout = "cudnn")
    labels = Input(LabelDim, tag = label)
    
    # Kernels width and height.
    kW = 3
    kH = 3
    # Kernel stride.
    hs = 1
    vs = 1
    
    # Pooling settings.
    poolW = 2
    poolH = 2
    poolhs = 2
    poolvs = 2
    
    # Initial parameter values.
    convWScale = 0.01 #7
    convBValue = 0
    fc1WScale = 0.01 #8
    fc1BValue = 0
    fc2WScale = 0.01 #3.2
    fc2BValue = 0
    fc3WScale = 0.01 #3.2
    fc3BValue = 0
]

DNN=[
    cMap1 = 64
    conv1 = ConvReLULayer(features, cMap1, 27, kW, kH, hs, vs, convWScale, convBValue)

    pool1 = MaxPooling(conv1, poolW, poolH, poolhs, poolvs, imageLayout = "cudnn")

    cMap2 = 128
    conv2 = ConvReLULayer(pool1, cMap2, 576, kW, kH, hs, vs, convWScale, convBValue)
   
    pool2 = MaxPooling(conv2, poolW, poolH, poolhs, poolvs, imageLayout = "cudnn")

    cMap3 = 256
    conv3 = ConvReLULayer(pool2, cMap3, 1152, kW, kH, hs, vs, convWScale, convBValue)
    conv4 = ConvReLULayer(conv3, cMap3, 2304, kW, kH, hs, vs, convWScale, convBValue)

    pool3 = MaxPooling(conv4, poolW, poolH, poolhs, poolvs, imageLayout = "cudnn")

    cMap5 = 512
    conv5 = ConvReLULayer(pool3, cMap5, 2304, kW, kH, hs, vs, convWScale, convBValue)
    conv6 = ConvReLULayer(conv5, cMap5, 4608, kW, kH, hs, vs, convWScale, convBValue)

    pool4 = MaxPooling(conv6, poolW, poolH, poolhs, poolvs, imageLayout = "cudnn")

    cMap6 = 512
    conv7 = ConvReLULayer(pool4, cMap6, 4608, kW, kH, hs, vs, convWScale, convBValue)
    conv8 = ConvReLULayer(conv7, cMap6, 4608, kW, kH, hs, vs, convWScale, convBValue)

    pool5 = MaxPooling(conv8, poolW, poolH, poolhs, poolvs, imageLayout = "cudnn")

    hiddenDim = 4096
    h1 = DnnReLULayer(25088, hiddenDim, pool5, fc1WScale, fc1BValue)
    h1_d = Dropout(h1)
    h2 = DnnReLULayer(hiddenDim, hiddenDim, h1_d, fc2WScale, fc2BValue)
    h2_d = Dropout(h2)
    ol = DnnLayer(hiddenDim, labelDim, h2_d, fc3WScale, fc3BValue)
    
    CE = CrossEntropyWithSoftmax(labels, ol, tag = Criteria)
    Err = ClassificationError(labels, ol, tag = Eval)
    OutputNodes = ol
]
