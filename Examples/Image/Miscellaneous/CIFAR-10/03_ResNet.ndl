load=LocalMacros
run=DNN

LocalMacros = [
    ImageW = 32
    ImageH = 32
    ImageC = 3
    LabelDim = 10

    features = ImageInput(ImageW, ImageH, ImageC, tag = feature, imageLayout = "cudnn")
    featOffs = Const(128)
    featScaled = Minus(features, featOffs)
    labels = Input(LabelDim, tag = label)
    
    convWScale = 7.07
    convBValue = 0
    fc1WScale = 12
    fc1BValue = 0
    
    scValue = 1
    
    expAvg = 1
    
    kW = 3
    kH = 3
    
    hStride1 = 1
    vStride1 = 1
    hStride2 = 2
    vStride2 = 2
]

DNN=[
    cMap1 = 16
    conv1 = ConvBNReLULayer(featScaled, cMap1, 27, kW, kH, hStride1, vStride1, convWScale, convBValue, scValue, expAvg)

    rn1_1 = ResNetNode2(conv1, cMap1, 144, kW, kH, convWScale, convBValue, scValue, expAvg)
    rn1_2 = ResNetNode2(rn1_1, cMap1, 144, kW, kH, convWScale, convBValue, scValue, expAvg)
    rn1_3 = ResNetNode2(rn1_2, cMap1, 144, kW, kH, convWScale, convBValue, scValue, expAvg)

    cMap2 = 32
    rn2_1_Wproj = Parameter(cMap2, cMap1, init = fromFile, initFromFilePath = "$Proj16to32Filename$", needGradient = false)
    rn2_1 = ResNetNode2Inc(rn1_3, cMap2, 144, 288, kW, kH, convWScale, convBValue, scValue, expAvg, rn2_1_Wproj)
    rn2_2 = ResNetNode2(rn2_1, cMap2, 288, kW, kH, convWScale, convBValue, scValue, expAvg)
    rn2_3 = ResNetNode2(rn2_2, cMap2, 288, kW, kH, convWScale, convBValue, scValue, expAvg)

    cMap3 = 64
    rn3_1_Wproj = Parameter(cMap3, cMap2, init = fromFile, initFromFilePath = "$Proj32to64Filename$", needGradient = false)
    rn3_1 = ResNetNode2Inc(rn2_3, cMap3, 288, 576, kW, kH, convWScale, convBValue, scValue, expAvg, rn3_1_Wproj)
    rn3_2 = ResNetNode2(rn3_1, cMap3, 576, kW, kH, convWScale, convBValue, scValue, expAvg)
    rn3_3 = ResNetNode2(rn3_2, cMap3, 576, kW, kH, convWScale, convBValue, scValue, expAvg)
                
    # Global average pooling
    poolW = 8
    poolH = 8
    poolhStride = 1
    poolvStride = 1
    pool = AveragePooling(rn3_3, poolW, poolH, poolhStride, poolvStride, imageLayout = "cudnn")

    ol = DnnLastLayer(cMap3, labelDim, pool, fc1WScale, fc1BValue)
    
    CE = CrossEntropyWithSoftmax(labels, ol, tag = Criteria)
    Err = ErrorPrediction(labels, ol, tag = Eval)
    OutputNodes = ol
]

